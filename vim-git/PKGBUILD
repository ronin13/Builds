# Maintaner: Raghavendra D Prabhu raghu d0t prabhu 1 3 AT  mail of google

pkgbase=vim-git
pkgname=(vim vim-runtime)
_topver=7.3
_patchlevel=3
pkgver=20101108
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
makedepends=('python2' 'libxt' 'desktop-file-utils'
             'gettext' 'pkgconfig' 'rsync' 'sed')
options=(force)
source=(pythoncomplete.vim::http://www.vim.org/scripts/download_script.php\?src_id=10872
        vimrc archlinux.vim)
md5sums=('6e7adfbd5d26c1d161030ec203a7f243'
         'e57777374891063b9ca48a1fe392ac05'
         '10353a61aadc3f276692d0e17db1478e')

_gitroot="git://github.com/b4winckler/vim.git"
_gitname="vim"

_versiondir="vim${_topver//./}"
##### Build #####

build() {
   cd "$srcdir"
   msg "Connecting to GIT server...."

   if [ -d $_gitname ] ; then
     cd $_gitname && git pull origin
     msg "The local files are updated."
   else
     git clone $_gitroot $_gitname
   fi

   msg "GIT checkout done or server timeout"
   msg "Starting make..."

   rm -rf "$srcdir/$_gitname-build"
   git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
   cd "$srcdir/$_gitname-build"

  msg2 'Patching...'

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
      src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
      src/feature.h
  # fix python name
  sed -i -e 's|vi_cv_path_python, python[0-9]*|vi_cv_path_python, python2|' \
      src/configure.in

  make clean
  (cd src && autoconf)
  msg2 'Building...'


#  autoconf
   ./configure --prefix=/usr --localstatedir=/var/lib/vim --disable-nls \
     --mandir=/usr/share/man --with-compiledby=Raghu --disable-sysmouse --disable-selinux   \
     --with-features=big --disable-gpm --disable-acl --with-x=no --disable-largefile \
     --disable-gui --disable-multibyte --enable-cscope --disable-xsmp --disable-xsmp-interact \
     --disable-netbeans --enable-pythoninterp   --disable-gtktest
  make 
}

##### Packaging #####
#{{{ vim
package_vim() {
  pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
  depends=("vim-runtime>=${pkgver}-${pkgrel}" 'gpm' 'perl')
  conflicts=('gvim')

  #make
  cd ${srcdir}/vim-build
  make -j1 VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
#  rm -r ${pkgdir}/usr/share/vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim
  ln -s /usr/share/vim/${_versiondir}/doc/uganda.txt \
    ${pkgdir}/usr/share/licenses/vim/license.txt

#  mv ${pkgdir}/usr/share/vim ${srcdir}/runtime-install
#   mv ${pkgdir}/usr/share/vim ${srcdir}/runtime-install
}
#}}}

#{{{ vim-runtime
package_vim-runtime() {
  pkgdesc='Runtime for vim and gvim'
  backup=(etc/vimrc)

#  cd ${srcdir}/vim-build

  # Install the runtime split from gvim
  install -dm755 ${pkgdir}/usr/share/vim/
  mv ${srcdir}/vim-build/runtime ${pkgdir}/usr/share/vim/${_versiondir}

  # Don't forget logtalk.dict
#  install -Dm644 ${srcdir}/vim-build/runtime/ftplugin/logtalk.dict \
#    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

  # make Aaron happy
  install -Dm644 ${srcdir}/pythoncomplete.vim \
    ${pkgdir}/usr/share/vim/${_versiondir}/autoload/pythoncomplete.vim
  
  # rc files
  install -Dm644 ${srcdir}/vimrc ${pkgdir}/etc/vimrc
  install -Dm644 ${srcdir}/archlinux.vim \
    ${pkgdir}/usr/share/vim/vimfiles/archlinux.vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim-runtime
  ln -s /usr/share/vim/${_versiondir}/doc/uganda.txt \
    ${pkgdir}/usr/share/licenses/vim-runtime/license.txt
}
#}}}
# vim:set sw=2 sts=2 et:
